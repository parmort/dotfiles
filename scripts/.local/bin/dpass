#!/usr/bin/zsh

set -e

timeout=30

select_command() {
  local contents="$(pass show "$1")"
  local first_line="$(head -1 <<< "$contents")"
  local the_password
  local the_username
  local the_url
  typeset -A options
  typeset -a options_order
  if [[ ! "${first_line}" =~ ": " ]]; then
    options[copy\ password]="print ${first_line} | xclip -i -selection clipboard"
    options_order+="copy password"
    the_password="$first_line"
  fi
  grep -P '^[^:]+(?=: .)' <<< "$contents" | while read line; do
    field="${line%%: *}"
    value="${line#*: }"
    if [[ "$field" == "url" ]]; then
      options[open\ url]="xdg-open ${(q)value}"
      options_order+="open url"
      the_url="$value"
    else
      options[copy\ $field]="print ${(q)value} | xclip -i -selection clipboard"
      options_order+="copy $field"
      [[ "$field" == "username" ]] && the_username="$value"
    fi
  done
  options[show]="notify-send ${(q)contents}"
  options_order+="show"
  if [[ -n "$the_username" ]] && [[ -n "$the_password" ]]; then
    options[copy\ username\ \&\ password]="print ${(q)the_username} | xclip -i && print ${(q)the_password} | xclip -i -selection clipboard "
    options_order=("copy username & password" "${options_order[@]}")
    if [[ -n "$the_url" ]]; then
      options[copy\ username\ \&\ password,\ open\ url]="xdg-open ${(q)the_url} && print ${(q)the_username} | xclip -i && print ${(q)the_password} | xclip -i -selection clipboard"
      options_order=("copy username & password, open url" "${options_order[@]}")
    fi
  fi
  local selection="$(print -l ${options_order} | dmenu -p "$1")"
  if [[ -n "$selection" ]]; then
    eval "$options[${(q)selection}]"
    exit 0
  else
    exit 2
  fi
}

select_password() {
  find "$dir" -not -path '*/.git/*' -type f |
    grep '\.gpg$' |
    sed "s:$dir\(.*\)\.gpg$:\1:" |
    dmenu -p 'password'
}

main() {
  local dir="${PASSWORD_STORE_DIR:-$HOME/.password-store/}"
  local password_selection="$(select_password "$1")"
  if [[ -n "$password_selection" ]]; then
    select_command "$password_selection" "$1"
    sleep $timeout
  else
    exit 1
  fi
}

main "$@"
